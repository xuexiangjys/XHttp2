apply plugin: 'com.android.application'
apply plugin: 'com.xuexiang.xaop' //引用xaop插件
apply plugin: 'com.xuexiang.xrouter' //引用xrouter-plugin插件实现自动注册

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    
    defaultConfig {
        applicationId "com.xuexiang.xhttp2demo"
        minSdkVersion 19
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    if (isNeedPackage.toBoolean()) {
        signingConfigs {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release
            }
        }
    }

    if (isNeedPackage.toBoolean()) {
        applicationVariants.all { variant ->
            variant.outputs.all {
                if (variant.buildType.name.equals('release')) {
                    outputFileName = "xhttp2_demo_${defaultConfig.versionName}.apk"
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }
    
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso.core

    //SmartRefreshLayout
    implementation deps.support.app_compat
    implementation deps.support.recyclerview
    implementation deps.support.design
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-layout:1.1.2'

    //XUtil
    implementation 'com.github.xuexiangjys.XUtil:xutil-core:1.1.4'
    implementation 'com.github.xuexiangjys.XUtil:xutil-sub:1.1.4'
    //rxutil2
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.github.xuexiangjys:rxutil2:1.1.2'
    //XAOP
    implementation 'com.github.xuexiangjys.XAOP:xaop-runtime:1.0.1'
    //XRouter
    implementation 'com.github.xuexiangjys.XRouter:xrouter-runtime:1.0.1'
    annotationProcessor 'com.github.xuexiangjys.XRouter:xrouter-compiler:1.0.1'
    //XPage
    implementation 'com.github.xuexiangjys.XPage:xpage-lib:2.2.2'
    annotationProcessor 'com.github.xuexiangjys.XPage:xpage-compiler:2.2.2'
    //butterKnife的sdk
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //XHttp
    implementation project(':xhttp2-lib')
    //RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    //OkHttp的依赖包
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    //Gson序列化反序列化的依赖包
    implementation deps.gson

    implementation 'com.github.gturedi:stateful-layout:1.2.1'

    implementation deps.glide

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
}
